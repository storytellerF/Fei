plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.10'
}

android {
    namespace 'com.storyteller_f.fei'
    compileSdk 33

    defaultConfig {
        applicationId "com.storyteller_f.fei"
        minSdk 26
        targetSdk 33
        versionCode 5
        versionName "1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    def javaVersion = JavaVersion.VERSION_17
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
    kotlinOptions {
        jvmTarget = javaVersion.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            pickFirsts += ['META-INF/*', '/META-INF/io.netty.versions.properties']
        }
        jniLibs {
            pickFirsts += ['META-INF/*']
        }
    }
}

dependencies {
    def compose_material3 = "1.0.1"
    def nav_version = "2.5.3"
    def ktor_version = '2.3.1'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    implementation "androidx.compose.material3:material3:$compose_material3"
    debugImplementation "androidx.customview:customview:1.1.0"
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0"

    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.30.0'

    implementation "androidx.navigation:navigation-compose:$nav_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    //ktor
    implementation "io.ktor:ktor-server-core:" + ktor_version
    implementation "io.ktor:ktor-server-netty:$ktor_version"

    implementation("io.ktor:ktor-server-host-common:$ktor_version")
    implementation("io.ktor:ktor-server-status-pages:$ktor_version")
    implementation("io.ktor:ktor-server-content-negotiation:$ktor_version")
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")
    implementation("io.ktor:ktor-server-thymeleaf:$ktor_version")
    implementation("io.ktor:ktor-server-html-builder:$ktor_version")
    implementation("io.ktor:ktor-server-websockets:$ktor_version")
    implementation("io.ktor:ktor-server-call-logging:$ktor_version")
    implementation("io.ktor:ktor-server-partial-content:$ktor_version")
    implementation("io.ktor:ktor-server-auto-head-response:$ktor_version")
    implementation("io.ktor:ktor-server-auth:$ktor_version")
    implementation("io.ktor:ktor-server-sessions:$ktor_version")

    implementation("io.ktor:ktor-client-websockets:$ktor_version")
    implementation("io.ktor:ktor-client-cio:$ktor_version")

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")


    implementation 'org.slf4j:slf4j-android:1.7.36'
    implementation 'com.google.zxing:core:3.5.1'

    implementation "com.github.JamalMulla:ComposePrefs3:1.0.3" // Current is 1.0.3
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation('androidx.webkit:webkit:1.7.0')
    implementation("androidx.browser:browser:1.5.0")

    implementation("androidx.core:core-splashscreen:1.0.1")
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.11'

    def baoFolder = project.ext.baoFolder
    def baoModule = findProject(":bao:startup")
    if (baoFolder == "local" && baoModule != null)
        implementation(baoModule)
    else
        implementation("com.github.storytellerF.Bao:startup:2.3.0")
}
